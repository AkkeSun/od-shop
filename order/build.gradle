buildscript {
    ext {
        protobufVersion = '4.27.2'
        protobufPluginVersion = '0.9.4'
        grpcVersion = '1.65.1'
    }
}
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.epages.restdocs-api-spec' version '0.19.0'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}


group = 'com'
version = '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // common module
    implementation project(':common')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // circuit breaker
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'
    implementation 'commons-codec:commons-codec:1.15'

    // swagger
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // springboot 3 jwt
    implementation "io.jsonwebtoken:jjwt:0.9.1"
    implementation "javax.xml.bind:jaxb-api:2.3.0" // java 11 부터는 의존성을 직접 주입 해주어야합니다.

    // kafka
    implementation 'org.springframework.kafka:spring-kafka'

    // grpc
    implementation "com.google.protobuf:protobuf-java-util:3.25.3"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"

    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'it.ozimov:embedded-redis:0.7.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

sourceSets {
    main {
        resources {
            srcDirs "src/main/deployment/${profile}"
        }
    }
}


test {
    useJUnitPlatform()
}

swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

// ./gradlew clean generateProto
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// ./gradlew :order:openapi3 :order:copyOasToSwagger
openapi3 {
    servers = [
            { url = "https://api.odlab.kr" }
    ]
    title = "Order API"
    description = "주문 정보를 관리하는 API 입니다"
    outputFileNamePrefix = 'order-api'
    version = "0.0.1"
    format = "yaml"
    outputDirectory = "src/main/resources/static/docs"
}

tasks.register('copyOasToSwagger', DefaultTask) {
    doLast {
        def openapi3File = file("src/main/resources/static/docs/order-api.yaml")
        def securitySchemesContent = "  securitySchemes:\n" +             \
                                                 "    APIKey:\n" +             \
                                                 "      type: apiKey\n" +             \
                                                 "      name: Authorization\n" +             \
                                                 "      in: header\n" +            \
                                                 "security:\n" +
                "  - APIKey: []"

        if (openapi3File.exists()) {
            def existingContent = openapi3File.text
            def updatedContent = existingContent + "\n" + securitySchemesContent
            openapi3File.text = updatedContent
            println "Added JWT authentication to ${openapi3File.path}"
        } else {
            println "Warning: openapi3.yaml not found at ${openapi3File.path}"
        }
    }
}

bootJar {
    archiveFileName = "order.jar"
}

jar {
    enabled = false
}
